# Maintainer: Christer Solskogen <christer.solskogen@gmail.com>
# Build order: aarch64-binutils -> aarch64-linux-api-headers -> aarch64-gcc-bootstrap -> aarch64-glibc -> aarch64-gcc -> aarch64-glibc (again)

pkgname=$TARGET-glibc
pkgver=2.33
pkgrel=1
_commit=be176490b818b65b5162c332eb6b581690b16e5c
pkgdesc='GNU C Library - compiled for $TARGET'
arch=('arm' 'armv6h' 'armv7h' 'aarch64' 'riscv64' 's390x' 'x86_64')
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
depends=()
makedepends=($TARGET-gcc $TARGET-linux-api-headers)
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz{,.sig})
sha256sums=('2e2556000e105dbd57f0b6b2a32ff2cf173bde4f0d85dffccfd8b7e51a0677ff'
            'SKIP')
validpgpkeys=(7273542B39962DF7B299931416792B4EA25340F8  # "Carlos O'Donell <carlos@systemhalted.org>"
              BC7C7372637EC10C57D7AA6579C43DFBF1CF2187) # Siddhesh Poyarekar

case $TARGET in
   arm)     _target=armv5tel-unknown-linux-gnueabi;;
   armv6h)  _target=armv6l-unknown-linux-gnueabihf;;
   armv7h)  _target=armv7l-unknown-linux-gnueabihf;;
   aarch64) _target=aarch64-unknown-linux-gnu;;
   riscv64) _target=riscv64-unknown-linux-gnu;;
   s390x)   _target=s390x-unknown-linux-gnu;;
   "") echo "You must set \$TARGET to a supported target." && exit 1;;
   *) echo "$TARGET is not a supported target!" && exit 1;;
esac


prepare() {
  mkdir -p glibc-build
}

build() {
  cd glibc-build
  echo "build-programs=no" > configparms
  echo "cross-compiling=yes" >> configparms
  echo "slibdir=/usr/lib" >> configparms
  echo "rtlddir=/usr/lib" >> configparms

  #hack - Having CPPFLAGS defined makes the build barf. Workaround it like this:
  _cppflags=$CPPFLAGS
  unset LD_LIBRARY_PATH CFLAGS CXXFLAGS CPPFLAGS
  
  ../glibc-$pkgver/configure \
      --prefix=/usr \
      --host=$_target \
      --libdir=/usr/lib \
      --with-bugurl=https://bugs.archlinux.org \
      --enable-kernel=4.4 \
      --enable-bind-now \
      --enable-stack-protector=strong \
      --enable-stackguard-randomization \
      --disable-timezone-tools \
      --enable-multi-arch CFLAGS="$_cppflags -O2 -pipe" 

  make
}

package() {
  cd glibc-build
  make DESTDIR="$pkgdir"/usr/$_target/sys-root install 

  #we don't want static libraries. Only keep the one that we really need.
  find "$pkgdir"/usr/$_target/sys-root  -name '*.a' -and -not -name libc_nonshared.a -delete
  
  #Remove files we don't need in a cross compilation environment 
  rm -r "$pkgdir"/usr/$_target/sys-root/{etc,usr/share,var}

  #strip manually
  find "$pkgdir"/usr/$_target/sys-root -name '*.so' -and ! -name 'libc.so' -print0 | xargs -0 $_target-strip --strip-all

  #workaround for fakeroot bug
  chown -R root:root "$pkgdir"/usr
}
