# Maintainer: Christer Solskogen <christer.solskogen@gmail.com>

pkgname=$TARGET-binutils
pkgver=2.44+r94+gfe459e33c676
_commit=fe459e33c676883b5f28cc96c00e242973d906a9
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files - cross for ${TARGET}"
arch=('arm' 'armv6h' 'armv7h' 'aarch64' 'riscv64' 's390x' 'x86_64')
url='https://www.gnu.org/software/binutils/'
license=(GPL-2.0-or-later GPL-3.0-or-later LGPL-2.0-or-later LGPL-3.0-or-later GFDL-1.3 FSFAP)
depends=(glibc jansson libelf zlib zstd)
makedepends=(git)
options=(staticlibs)
source=(git+https://sourceware.org/git/binutils-gdb.git#commit=${_commit})
sha256sums=('07a821f494fbb61dcf9e958f6f840eaa4a45c748c59415fd3ea1ec4b3326673c')
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)

pkgver() {
  cd binutils-gdb
  git describe --abbrev=12 --tags | sed 's/[^-]*-//;s/[^-]*-/&r/;s/-/+/g;s/_/./'
}

case $TARGET in
	arm)		_target=armv5tel-cross-linux-gnueabi;;
	armv6h)	_target=armv6l-cross-linux-gnueabihf;;
	armv7h)	_target=armv7l-cross-linux-gnueabihf;;
	aarch64|arm64) _target=aarch64-cross-linux-gnu;;
	riscv64) _target=riscv64-cross-linux-gnu;;
	s390x)	_target=s390x-cross-linux-gnu;;
	"") echo "You must set \$TARGET to a supported target." && exit 1;;
	*)	echo "$TARGET is not a supported target!" && exit 1;;
esac
prepare() {
  mkdir -p binutils-build
  cd binutils-gdb

  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh

}


build() {
  cd binutils-build
  "${srcdir}"/binutils-gdb/configure \
    --prefix=/usr \
    --sysconfdir="${pkgdir}"/etc \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --enable-cet \
    --enable-colored-disassembly \
    --enable-default-execstack=no \
    --enable-deterministic-archives \
    --enable-jansson \
    --enable-ld=default \
    --enable-new-dtags \
    --enable-plugins \
    --enable-relro \
    --enable-threads \
    --disable-werror \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib \
    --target=$_target \
    --with-sysroot=/usr/$_target/sys-root \
    --disable-multilib \
    --disable-nls
  
  make all-gas all-ld all-binutils
}

package() {
  cd binutils-build

  make DESTDIR="$pkgdir" install-gas install-ld install-binutils 

  # Remove files that we don't need for this package
  rm -rf "$pkgdir"/usr/{include,lib,share}
}
