# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>
# Build order: aarch64-binutils -> aarch64-linux-api-headers -> aarch64-gcc-bootstrap -> aarch64-glibc -> aarch64-gcc -> aarch64-glibc (again)

pkgname=$TARGET-gcc
pkgver=15.1.1+r7+gf36ec88aa85a
_commit=f36ec88aa85a1a8f4ec300dfcd862fc4fbca1c53
pkgrel=1
pkgdesc='The GNU Compiler Collection - cross compiler for ARM64 target'
arch=('arm' 'armv6h' 'armv7h' 'aarch64' 'riscv64' 's390x' 'x86_64')
url='https://gcc.gnu.org/'
license=(GPL LGPL FDL)
depends=(libmpc zstd libisl)
makedepends=(binutils $TARGET-binutils)
provides=($TARGET-gcc)
conflicts=($TARGET-gcc-bootstrap)
options=(!emptydirs !strip staticlibs)
source=(git+https://sourceware.org/git/gcc.git#commit=${_commit})

validpgpkeys=(F3691687D867B81B51CE07D9BBE43771487328A9  # bpiotrowski@archlinux.org
              86CFFCA918CF3AF47147588051E8B148A9999C34  # evangelos@foutrelis.com
              13975A70E63C361C73AE69EF6EEB81F8981C74C7  # richard.guenther@gmail.com
              D3A93CAD751C2AF4F8C7AD516C35B99309B5FA62) # Jakub Jelinek <jakub@redhat.com>
sha256sums=('7dc521dbaf5e8db83574db217c5fdb8c4fed6ec1cc6ce9f0095755f599890b8c')

pkgver() {
  cd gcc
  echo "$(cat gcc/BASE-VER)+$(git describe --abbrev=12 --tags | sed 's/[^-]*-[^-]*-//;s/[^-]*-/r&/;s/-/+/g;s/_/./')"
}

case $TARGET in
	arm)    
		_target=armv5tel-cross-linux-gnueabi
		configure_options=(--with-arch=armv5te --with-float=soft);;
	armv6h)
		_target=armv6l-cross-linux-gnueabihf
		configure_options=(--with-arch=armv6 --with-float=hard --with-fpu=vfp);;
	armv7h)
		_target=armv7l-cross-linux-gnueabihf
		configure_options=(--with-arch=armv7-a --with-float=hard --with-fpu=vfpv3-d16);;
	aarch64)
		_target=aarch64-cross-linux-gnu
		configure_options=(
			--with-arch=armv8-a
			--enable-fix-cortex-a53-835769
			--enable-fix-cortex-a53-843419)
		;;
	riscv64)
		_target=riscv64-cross-linux-gnu
		configure_options=();;
	s390x)
		_target=s390x-cross-linux-gnu
		configure_options=(--with-arch=z13);;
	"") echo "You must set \$TARGET to a supported target." && exit 1;;
	*) echo "$TARGET is not a supported target!" && exit 1;;
esac

build() {
  rm -rf "$srcdir"/gcc-build
  mkdir -p "$srcdir"/gcc-build
  cd "$srcdir"/gcc-build
  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  "$srcdir"/gcc/configure \
      --prefix=/usr \
      --with-sysroot=/usr/$_target/sys-root \
      --libexecdir=/usr/lib \
      --target=$_target \
      --disable-nls --enable-default-pie \
      --enable-languages=c,c++ \
      --with-system-zlib --enable-__cxa_atexit \
      --enable-linker-build-id \
      --enable-plugin \
      --with-linker-hash-style=gnu --enable-gnu-indirect-function \
      --disable-werror \
      --enable-checking=release \
      "${configure_options[@]}"

  make
}

package() {
  cd gcc-build

  make install-strip DESTDIR="$pkgdir"
  ln -s $_target-gcc "$pkgdir"/usr/bin/$_target-cc
  # Remove files that conflict with host gcc package
  rm -rf "$pkgdir"/usr/{include,share}
  rm -rf "$pkgdir"/usr/lib/libcc1.*

}
